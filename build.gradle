buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.jmix.gradle:jmix-gradle-plugin:2.5.0-RC1'
    }
}

group = 'gr.netmechanics.jmix'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven {
            url = 'https://nexus.jmix.io/repository/public'
        }
    }

    jmix {
        bomVersion = '2.5.0-RC1'
        projectId = 'tinymce'
    }

    group = this.group
    version = this.version

    def props = new Properties()
    buildFile.withInputStream { props.load(it) }
    def subArchivesBaseName = props.getProperty('archivesBaseName')
    def archName = subArchivesBaseName.substring(1, subArchivesBaseName.length() - 1)

    java {
        withSourcesJar()
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        repositories {
            if (project.hasProperty("nexus_username")) {
                maven {
                    name = "NetmechanicsNexus"
                    url = 'https://nexus.nmlabs.gr/repository/nmplatform/'
                    credentials {
                        username = "${nexus_username}"
                        password = "${nexus_password}"
                    }
                }
            } else {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY"))
                    credentials {
                        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                        password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                    }
                }
            }
        }
        publications {
            javaMaven(MavenPublication) {
                artifactId = archName
                from components.java
            }
        }
    }

    // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs.add("-parameters")
    }
}
